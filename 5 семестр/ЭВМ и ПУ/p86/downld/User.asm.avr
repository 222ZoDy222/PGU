;***************************************************************************
;Файл для процедур пользователя.
;Код, производящий вычисления с использованием команд MMX, следует
;писать внутри процедуры MMX_proc. Код, производящий вычисления без
;использования команд MMX - внутри процедуры Simp_proc.
;Имена процедур изменять нельзя.
;Параметры в процедуры передаются через стек:
; - по адресу +6 хранится смещение вектора a_vector (состоит из слов)
;   в сегменте данных;
; - по адресу +8 хранится смещение вектора b_vector (состоит из слов)
;   в сегменте данных;
; - по адресу +10 хранится количество элементов в векторах (вектора
;   одинакового размера).
;Результаты вычислений с использованием команд MMX записываются в массив
; Result_mmx,результаты вычислений без использования команд MMX - в масси
;в Result_simp. В переменную NumResArray записывается количество элементов
;в результирующем массиве. В переменную ElemSize записывается размер (0 - байт,
;1 - слово, 2 - двойное слово) элемента результирующего массива.
;****************************************************************************

.model MEDIUM
;экспорторуемые данные
PUBLIC  MMX_proc,Simp_proc,OffsResMmx,OffsResSimp,NumResArray,ElemSize

STACK   SEGMENT PARA stack 'STACK'
        db 400h dup (?)
STACK   ENDS

DATA    SEGMENT PARA USE16 PUBLIC 'DATA'
NumResArray     DW      ?               ;количество элементов в результирующем массиве
ElemSize        DB      0FFh
Result_mmx      DD      40 DUP(?)       ;место в памяти для записи результата вычислений процедуры с командами MMX
Result_simp     DD      40 DUP(?)       ;место в памяти для записи результата вычислений процедуры без команд MMX
OffsResMmx      DW      Result_mmx      ;смещение начала массива Result_mmx
OffsResSimp     DW      Result_simp     ;смещение начала массива Result_simp
count           DW      ?
temp            DQ      0h
DATA    ENDS

CODE    SEGMENT
        ASSUME cs:code, ds:data, ss:stack
        .586
        .mmx
;***************************************************************************
; Процедура нахождения среднего арифметического для вектора с использ. MMX *
;***************************************************************************
MMX_proc        proc    far
        push    bp
        mov     bp,sp
        mov     si,[bp+6]               ;в si адрес вектора a_vector
        mov     cx,[bp+10]              ;в cx количество элементов вектора
        movq    mm2,[si]
        movq    mm3,mm2
        punpcklwd       mm2,temp
        punpckhwd       mm3,temp
        add     si,8
        dec     cx
        shr     cx,2
@@Loop1:
        movq    mm0,[si]
        movq    mm1,mm0
        punpcklwd       mm0,temp
        punpckhwd       mm1,temp
        paddd   mm2,mm0
        paddd   mm3,mm1
        add     si,8
        loop    @@Loop1
        paddd   mm2,mm3
        movq    mm3,mm2
        psrlq   mm3,32
        paddd   mm2,mm3

        movd    dword ptr [Result_mmx],mm2
        emms
        mov     ax,word ptr [Result_mmx]
        mov     dx,word ptr [Result_mmx+2]
        mov     cx,[bp+10]
        idiv    cx
        mov     word ptr [Result_mmx],ax
        mov     NumResArray,1           ;результат - число (один элемент массива)
        mov     ElemSize,1              ;размер элемента массива - слово
        pop     bp
        ret     6                       ;выход с очисткой стека
MMX_proc        endp
;*****************************************************************************
; Процедура нахождения среднего арифметического для вектора без использ. MMX *
;*****************************************************************************
Simp_proc       proc    far
        push    bp
        mov     bp,sp
        mov     si,[bp+6]
        mov     cx,[bp+10]
        xor     ax,ax
        xor     bx,bx
        xor     di,di
@@Loop2:
        mov     ax,word ptr[si]
        cwd
        add     bx,ax
        adc     di,dx
        add     si,2
        loop    @@Loop2
        mov     ax,bx
        mov     dx,di
        mov     cx,[bp+10]
        idiv    cx
        mov     word ptr [Result_simp],ax
        mov     NumResArray,1
        mov     ElemSize,1
        pop     bp
        ret     6
Simp_proc       endp

CODE    ENDS
        END
